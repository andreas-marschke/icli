#!/usr/bin/env perl
use autodie;
use strict;
use warnings;
use 5.010;

use Term::ANSIColor;

my ($data, $cache);
my $status_file = '/var/lib/icinga/status.dat';
my $context;

sub read_status_line {
	my ($line) = @_;

	if ($line =~ / ^ (?<context> \w+) \s+ { /x) {
		$context = $+{context};
	}
	elsif ($line =~ / ^ \t* (?<key> [^=]+ ) = (?<value> .*) $ /x) {
		$cache->{$+{key}} = $+{value};
	}
	elsif ($line =~ / ^ \t* } \s* $ /x) {
		given($context) {
			when(['info', 'programstatus']) {
				$data->{$context} = $cache;
			}
			when('hoststatus') {
				push(@{$data->{hosts}}, $cache);
			}
			when('servicestatus') {
				push(@{$data->{services}->{$cache->{host_name}}}, $cache);
			}
			when('contactstatus') {
				push(@{$data->{contacts}}, $cache);
			}
			default {
				warn("Unknown field in $status_file: $context\n");
			}
		}
		$cache = undef;
	}
}

sub read_status {
	open(my $fh, '<', $status_file);
	while (my $line = <$fh>) {
		chomp($line);
		read_status_line($line);
	}
	close($fh);
}

sub state_to_string {
	my ($digit) = @_;
	given ($digit) {
		when(0) { return colored('   OK   ', 'black on_green' ) }
		when(1) { return colored(' WARNING', 'black on_yellow') }
		when(2) { return colored('CRITICAL', 'white on_red'   ) }
		when(3) { return colored(' UNKNOWN', 'white on_blue'  ) }
		default { return colored('  ???   ', 'white'          ) }
	}
}

read_status();

foreach my $host (sort keys %{$data->{services}}) {
	say "\n$host";
	foreach my $s (@{$data->{services}->{$host}}) {
		printf(
			"\t%-20.20s %s  %s\n",
			$s->{service_description},
			state_to_string($s->{current_state}),
			$s->{plugin_output},
		);
	}
}

__END__

=head1 NAME

=head1 SYNOPSIS

=head1 DESCRIPTION

=head1 OPTIONS

=head1 EXIT STATUS

=head1 CONFIGURATION

=head1 DEPENDENCIES

=head1 BUGS AND LIMITATIONS

=head1 AUTHOR

Copyright (C) 2010 by Daniel Friesel E<lt>derf@chaosdorf.deE<gt>

=head1 LICENSE

  0. You just DO WHAT THE FUCK YOU WANT TO.
